
# depending on your Qt configuration, you want to enable or disable
# one of the release/debug builds (if all three lines are commented,
# the default of your Qt installation will used)

# build with both debug and release mode
#CONFIG += debug_and_release build_all

# build with release mode only
#CONFIG += release

# build with debug mode only
#CONFIG += debug

# for all debug builds, add "_d" extension to target
CONFIG(debug, debug|release) {
  TARGET = $${TARGET}_d 
}

# Allow override (e.g., qmake PYTHONQT_GENERATED_PATH=/abs/path)
isEmpty(PYTHONQT_GENERATED_PATH) {
  PYTHONQT_GENERATED_PATH = $$PWD/../generated_cpp
}

# Fallbacks to checked-in wrappers
!exists($$PYTHONQT_GENERATED_PATH) {
  # Exact version directory (e.g., generated_cpp_515 or generated_cpp_69)
  PYTHONQT_GENERATED_PATH_VERSIONED = $$PWD/../generated_cpp_$${QT_MAJOR_VERSION}$${QT_MINOR_VERSION}
  exists($$PYTHONQT_GENERATED_PATH_VERSIONED) {
    PYTHONQT_GENERATED_PATH = $$PYTHONQT_GENERATED_PATH_VERSIONED
  }
}

!exists($$PYTHONQT_GENERATED_PATH) {
  # For Qt5 we know that the older generated wrappers work with the later
  # versions, even (apparently) Qt5.15, so:
  equals(QT_MAJOR_VERSION, 5) {
    # Qt5: have 5.0, 5.3, 5.4, 5.6 and 5.11 at present:
    lessThan(QT_MINOR_VERSION, 3) { # 5.1, 5.2
      PYTHONQT_GENERATED_PATH_VERSIONED = $$PWD/../generated_cpp_50
    }
    else: lessThan(QT_MINOR_VERSION, 6) { # 5.3, 5.4, 5.5
      PYTHONQT_GENERATED_PATH_VERSIONED = $$PWD/../generated_cpp_54
    }
    else: lessThan(QT_MINOR_VERSION, 11) { # 5.6, 5.7, 5.8, 5.9, 5.10
      PYTHONQT_GENERATED_PATH_VERSIONED = $$PWD/../generated_cpp_56
    }
    else: lessThan(QT_MINOR_VERSION, 15) { # 5.11, 5.12, 5.13, 5.14
      PYTHONQT_GENERATED_PATH_VERSIONED = $$PWD/../generated_cpp_511
    }
    else { # >= 5.15
      PYTHONQT_GENERATED_PATH_VERSIONED = $$PWD/../generated_cpp_515
    }
    exists($$PYTHONQT_GENERATED_PATH_VERSIONED) {
      PYTHONQT_GENERATED_PATH = $$PYTHONQT_GENERATED_PATH_VERSIONED
    }
  }
}

!exists($$PYTHONQT_GENERATED_PATH) {
  error("No generated wrapper sources for Qt $${QT_VERSION}.\n" \
        "Looked in:\n" \
        "  - $$PWD/../generated_cpp\n" \
        "  - $$PWD/../generated_cpp_$${QT_MAJOR_VERSION}$${QT_MINOR_VERSION} (e.g., generated_cpp_515)\n" \
        "Generate them first:\n" \
        "  qmake CONFIG+=generator_only && make\n" \
        "  cd generator && ./pythonqt_generator\n" \
        "Or pass an explicit path:\n" \
        "  qmake PYTHONQT_GENERATED_PATH=/abs/path")
}

!build_pass {
  message("Qt version: Qt$${QT_VERSION}")
  message("Using generated sources files from $${PYTHONQT_GENERATED_PATH}")
}

VERSION = 3.2.0
win32: CONFIG += skip_target_version_ext
gcc|win32-clang-msvc:QMAKE_CXXFLAGS += -Wno-deprecated-declarations -Wuninitialized -Winit-self -pedantic
win32-clang-msvc:QMAKE_CXXFLAGS += -Wno-unused-command-line-argument
#Do not issue warning to system includes
gcc:!isEmpty(QT_INSTALL_HEADERS): QMAKE_CXXFLAGS += -isystem $$[QT_INSTALL_HEADERS]
